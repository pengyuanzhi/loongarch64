#define ASM_USE
#include <asm.h>
/************************宏 定 义******************************/

/************************类型定义******************************/

/************************外部声明******************************/

/************************前向声明******************************/

/************************模块变量******************************/

/************************函数实现******************************/

.text

/*
* @brief
*    将target指向的值增加value，并返回target原有指向的值。
* @param[out]||[in]: target: 需要增加值的地址。
* @param[in]: value: 需要增加的值
* @retval target原有指向的值
*/
/* T_UWORD atomic32_add(T_UWORD * target, T_UWORD value) */
ENTRY(atomic32_add)
	amadd_db.w t0,a1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_add)	

/*
* @brief
*	 将target指向的值增加value，并返回target增加后的值。
* @param[out]||[in]: target: 需要增加值的地址。
* @param[in]: value: 需要增加的值
* @retval target增加后的值
*/
/* T_UWORD atomic32_add_return(T_UWORD * target, T_UWORD value) */
ENTRY(atomic32_add_return)
	amadd_db.w t0,a1,a0
    add.w t0,t0,a1
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_add_return)

/*
* @brief
*       将target指向的值与value位或，并返回target原有的值。
* @param  target: 需要位或值的地址。
* @param[in]  value: 需要位或的值。
* @return target原有指向的值。
*/
/* T_UWORD atomic32Or(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32Or)
	amor_db.w t0,a1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32Or)
     
/*
* @brief
*       将target指向的值与value位与，并返回target原有的值。
* @param  target: 需要位与值的地址。
* @param[in]  value: 需要位与的值。
* @return target原有指向的值。
*/
/* T_UWORD atomic32And(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32And)
	amand_db.w t0,a1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32And)

/*
* @brief
*       设置target指向的值为value，并返回target原有的值。
* @param  target: 需要设置值的地址。
* @param[in]  value: 需要设置的值。
* @return target原有指向的值。
*/
/* T_UWORD atomic32_set(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32_set)
	amswap_db.w t0,a1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_set)

/*
* @brief
*       获取target指向的值。
* @param  target: 需要获取值的地址。
* @return target指向的值。
*/
/*
* @brief
*       获取target指向的值。
* @param  target: 需要获取值的地址。
* @return 获取target指向的值
*/
/* T_UWORD atomic32Get(T_UWORD * target) */
ENTRY(atomic32Get)
	amadd_db.w t0,zero,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32Get)

/*
* @brief
*       将target指向的值增加1，并返回target原有的值。
* @param  target: 需要增加值的地址。
* @return target原有指向的值
*/
/* T_UWORD atomic32_inc(T_UWORD * target) */
ENTRY(atomic32_inc)
	li.w 	t1,1
	amadd_db.w t0,t1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_inc)

/*
* @brief
*		将target指向的值增加1，并返回target增加后的值。
* @param  target: 需要增加值的地址。
* @return target增加后的值
*/
/* T_UWORD atomic32_inc_return(T_UWORD * target) */
ENTRY(atomic32_inc_return)
	li.w 	t1,1
	amadd_db.w t0,t1,a0
    add.w t0,t0,t1
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_inc_return)

/*
* @brief
*       将target指向的值减去1，并返回target原有的值。
* @param  target: 需要减去值的地址。
* @return target原有指向的值
*/
/* T_UWORD atomic32_dec(T_UWORD * target) */
ENTRY(atomic32_dec)
	li.w t1,-1
	amadd_db.w t0,t1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_dec)

/*
* @brief
*		将target指向的值减去1，并返回target减去后的值。
* @param  target: 需要减去值的地址。
* @return target减去后的值
*/
/* T_UWORD atomic32_dec_return(T_UWORD * target) */
ENTRY(atomic32_dec_return)
	li.w t1,-1
	amadd_db.w t0,t1,a0
    add.w t0,t0,t1
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_dec_return)

/*
* @brief
*       将target指向的值与oldValue对比，如果相等，设置target指向的值为newValue，
*       并返回TRUE；如果不相等，则直接返回FALSE。
* @@param  target: 需要对比值的地址。
* @param[in]  oldValue: 需要对比的值。
* @param[in]  newValue: 需要设置的值。
* @return TURE:target指向的值与oldValue相等
* @return FALSE:target指向的值与oldValue不相等
*/
/* BOOL atomic32_cas(T_UWORD * target,T_UWORD oldValue,T_UWORD newValue) */
ENTRY(atomic32_cas)
/*    li.w      t1, -1 */       /* set initial result status to FALSE */
    li.w      t1,  0       /* set initial result status to FALSE */
1:
    dbar 0
    ll.w    t0, a0, 0       /* get current value and atomic check */
    bne     t0, a1, 2f
    nop
    move    t2, a2
    sc.w    t2, a0, 0
    beqz    t2, 1b          /* check if atomic op was successful */
    nop
/*    li.w      t1, 0 */        /* store worked, return TRUE */
    li.w      t1, 1        /* store worked, return TRUE */
2:
    dbar 0
    move	a0, t1
	jirl zero,ra,0
	nop
ENDPROC(atomic32_cas)

/*
* @brief
*       将target指向的值减去value，并返回target原有的值。
* @param  target: 需要减去值的地址。
* @param[in]  value: 需要减去的值。
* @return target原有指向的值
*/
/* T_UWORD atomic32_sub(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32_sub)
1:
    dbar 0
    ll.w     t1, a0,0       /* get current value and atomic check */
    sub.w   t0, t1, a1
    sc.w     t0, a0,0
    beqz    t0, 1b          /* check if atomic op was successful */
	move	a0,t1
	jirl zero,ra,0
	nop
ENDPROC(atomic32_sub)

/*
* @brief
*		将target指向的值减去value，并返回target减去后的值。
* @param  target: 需要减去值的地址。
* @param[in]  value: 需要减去的值。
* @return target减去后的值
*/
/* T_UWORD atomic32_sub_return(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32_sub_return)
1:
    dbar 0
    ll.w     t1, a0,0       /* get current value and atomic check */
    sub.w   t0, t1, a1
    sc.w     t0, a0,0
    beqz    t0, 1b          /* check if atomic op was successful */
	move	a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32_sub_return)

/*
* @brief
*       清除target指向的值，并返回target原有的值。
* @param  target: 需要清除值的地址。
* @return target原有指向的值
*/
/* T_UWORD atomic32Clear(T_UWORD * target) */
ENTRY(atomic32Clear)
	amswap_db.w t0,zero,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32Clear)

/*
* @brief
*       将target指向的值与value位异或，并返回target原有的值。
* @param  target: 需要位异或值的地址。
* @param[in]  value: 需要位异或的值。
* @return target原有指向的值
*/
/* T_UWORD atomic32Xor(T_UWORD * target,T_UWORD value) */
ENTRY(atomic32Xor)
	amxor_db.w t0,a1,a0
	move a0,t0
	jirl zero,ra,0
	nop
ENDPROC(atomic32Xor)
