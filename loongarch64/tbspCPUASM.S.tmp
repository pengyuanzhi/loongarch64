/************************************************************************
 *              北京科银京成技术有限公司 版权所有
 *   Copyright (C) 2011 CoreTek Systems Inc. All Rights Reserved.
 ***********************************************************************/

/*
 * 修改历史：
 * 2024-08-28    zyh，科东（广州）软件科技有限公司
 *               创建文件
 */

/************************头 文 件******************************/
#define ASM_USE
#include <cpu.h>
#include <asm.h>
#include <ttosHal.h>

/*
 * @brief:
 *    进入IDLE任务函数。
 * @param[in]: stackTop: IDLE任务栈
 * @return:
 *    无
 */
/*  T_VOID idleTaskEntryLoad(T_VOID *stackTop)*/
ENTRY(idleTaskEntryLoad)
    BACKUP_T0T1
    li.d    t0,32
    sub.d   a0,a0,t0 /*预留安全空间*/
    srli.d  a0,a0,4
    slli.d  a0,a0,4 /*保证栈地址是16字节对齐的*/
    move sp,a0
    RELOAD_T0T1
    /* 调用入口函数 */
    bl ttosIdleTaskEntry
1:
    b 1b

ENDPROC(idleTaskEntryLoad)

/*
 * @brief:
 *    获取优先级位图中从最高有效位(bit0)开始第一个为1的位所在位数。
 * @param[in]: priBitMap: 优先级位图
 * @returns:
 *    无。
 * @tracedREQ: RT.6
 * @implements: DT.6.14.PPC
 */
/* T_UWORD tbspGetMSB(T_UWORD priBitMap) */
.text
.align  4
.global tbspGetMSB
tbspGetMSB:
    /* TODO:jcai 不知道实现对不 */
    clz.w a0, a0
    jirl zero,ra,0

/*
 * @brief:
 *    加载任务的栈指针。
 * @param[in]: stackTop: 要加载的任务栈底值
 * @return:
 *    无
 * @tracedREQ: RT.6
 * @implements: DT.6.15.PPC
 */
/*T_VOID tbspLoadStack(T_VOID *stackTop)*/
.text
.align  4
.global tbspLoadStack
tbspLoadStack:
    BACKUP_T0T1
    li.d    t0,32
    sub.d   a0,a0,t0
    srli.d  a0,a0,4
    slli.d  a0,a0,4 /*保证栈地址是16字节对齐的*/
    move    sp,a0
    RELOAD_T0T1
    jirl zero,ra,0
/*
 * @brief:
 *    保存任务的运行上下文。
 * @param[out]: context: 任务上下文
 * @return:
 *    0: 当任务被切换走，保存上下文后。
 *    1: 当任务被切换回，恢复上下文后。
 * @tracedREQ: RT.6
 * @implements: DT.6.16.PPC
 */

/*T_UWORD tbspSaveTaskContext(T_TBSP_TaskContext *context)*/

.text
.align  4
.global tbspSaveTaskContext
tbspSaveTaskContext:
    task_context_save a0
    BACKUP_T0T1
    csrrd    t0,LOONGARCH_CSR_CRMD
    st.d     t0,a0,THREAD_CRMD
    csrrd    t0,LOONGARCH_CSR_PRMD
    st.d     t0,a0,THREAD_PRMD
    csrrd    t0,LOONGARCH_CSR_EUEN
    st.d     t0,a0,THREAD_EUEN
    csrrd    t0,LOONGARCH_CSR_ECFG
    st.d     t0,a0,THREAD_ECFG
    csrrd    t0,LOONGARCH_CSR_ESTAT
    st.d     t0,a0,THREAD_ESTAT
    csrrd    t0,LOONGARCH_CSR_BADV
    st.d     t0,a0,THREAD_BADV
    st.d     tp,a0,THREAD_TLS
    csrrd    t0,LOONGARCH_CSR_PGDL
    st.d     t0,a0,THREAD_PGDL
    csrrd    t0,LOONGARCH_CSR_ASID
    st.d     t0,a0,THREAD_ASID
    csrrd    t0,LOONGARCH_CSR_ERA
    st.d     t0,a0,THREAD_PC
    st.d     sp,a0,THREAD_SP
    ld.d     t0,a0,THREAD_VFLAG  /* save vintflag */
    csrwr    t0,LOONGARCH_CSR_KS4
#ifdef _HARD_FLOAT_
    addi.d    a0,a0,THREAD_FPU
    fpu_save_csr    a0 t0
    fpu_save_double a0 t1
    fpu_save_cc     a0 t0 t1
#endif /* _HARD_FLOAT_ */
   RELOAD_T0T1
    li.w        a0,0
    jirl zero,ra,0




.text
.align  4
.global tbspRestoreTaskContext
tbspRestoreTaskContext:
    task_context_restore a0
    BACKUP_T0T1
    ld.d     t0,a0,THREAD_CRMD
    csrwr    t0,LOONGARCH_CSR_CRMD
    ld.d     t0,a0,THREAD_PRMD
    csrwr    t0,LOONGARCH_CSR_PRMD
    ld.d     t0,a0,THREAD_EUEN
    csrwr    t0,LOONGARCH_CSR_EUEN
    ld.d     t0,a0,THREAD_ECFG
    csrwr    t0,LOONGARCH_CSR_ECFG
    ld.d     t0,a0,THREAD_ESTAT
    csrwr    t0,LOONGARCH_CSR_ESTAT
    ld.d     t0,a0,THREAD_BADV
    csrwr    t0,LOONGARCH_CSR_BADV
    ld.d     tp,a0,THREAD_TLS
    ld.d     t0,a0,THREAD_PC
    csrwr    t0,LOONGARCH_CSR_ERA

    ld.d     t0,a0,THREAD_PGDL
    csrwr    t0,LOONGARCH_CSR_PGDL
    ld.d     t0,a0,THREAD_ASID
    csrwr    t0,LOONGARCH_CSR_ASID

    ld.d     sp,a0,THREAD_SP
    csrrd    t0,LOONGARCH_CSR_KS4    /* restore vintflag*/
    st.d     t0,a0,THREAD_VFLAG
#ifdef _HARD_FLOAT_
    addi.d    a0,a0,THREAD_FPU
    fpu_restore_double  a0 t0
    fpu_restore_csr     a0 t0 t1
    fpu_restore_cc      a0 t0 t1
#endif /* _HARD_FLOAT_ */
    RELOAD_T0T1
    li.w        a0,1
    jirl        zero,ra,0

#ifdef _HARD_FLOAT_

.text
.align  4
.global fpscrAsmInit
fpscrAsmInit:
    /* TODO:jcai 未实现 */
    jirl zero,ra,0

#endif /* _HARD_FLOAT_ */
